#BNF+IAT 1.0 UTF-8;

//////////////////////////////////////开关设置//////////////////////////////////////
/***********************************************************************************
//最多常用联系人数量，大于该数量时槽位不更新
MAX_USUAL_CONTACT_NUMBER = 5000
//最多非常用联系人数量，大于该数量时槽位不更新
MAX_UNUSUAL_CONTACT_NUMBER = 5000
//最多常用别名数量，大于该数量时槽位不更新
MAX_USUAL_ALIAS_NUMBER = 5000
//最多非常用别名数量，大于该数量时槽位不更新
MAX_UNUSUAL_ALIAS_NUMBER = 5000
//混乱字符槽位更新数量，为0时不更新
CHAOS_CHAR_SLOT_NUMBER=0
***********************************************************************************/
/////////////////////////////////////////////////////////////////////////////////////

!grammar %GRAMMAR_ID%;

!slot <preActive>;
!slot <prePassive>;
!slot <preTell>;
!slot <prePlease>;

!slot <cmuKeywords>;

!slot <cmdKeywords>;
!slot <cmdAppOpen>;
!slot <cmdAppNames>;

!slot <callActToSuf>;
!slot <callPhoneSuf>;
!slot <callCmd>;
!slot <callTo>;
!slot <callOf>;
!slot <callCon1>; //原始常用联系人槽位
!slot <callCon2>; //原始非常用联系人槽位
!slot <callUsual>; //常用别名槽位
!slot <callUnusual>; //非常用别名槽位
!slot <callService>; //服务号槽位
!slot <callEnglish>; //英文名槽位
!slot <callPrefix>;
!slot <callSuffix>;
!slot <callPro>;
!slot <callPhoneA>;
!slot <callPhoneZ>;
!slot <callPhoneM>;
!slot <callPhoneS>;

!slot <navNav>;
!slot <navCmdWord>;
!slot <navToWords>;
!slot <navSuffix>;
!slot <navHome>;
!slot <navCompany>;
!slot <navPOI>;
!slot <navNearAct>;
!slot <navNearKw>;

!slot <mscCmd>;
!slot <mscAmount>;
!slot <mscSinger>;
!slot <mscAlbum>;
!slot <mscList>;
!slot <mscSong>;
!slot <mscOf>;
!slot <mscType>;
!slot <mscTSuf>;
!slot <mscASuf>;
!slot <mscSSuf>;

!slot <nwsKw>;
!slot <nwsNews>;

!slot <askMscNow>;
!slot <askMscPlay>;
!slot <askMscOf>;
!slot <askMscIs>;
!slot <askMscWhat>;
!slot <askMscMsc>;

!slot <allDigit>;
!slot <allAlpha>;

!slot <allCh01>;
!slot <allCh02>;
!slot <allCh03>;
!slot <allCh04>;
!slot <allCh05>;

!start <txzStart>;

//////////////////////////////////////命令开始//////////////////////////////////////

<txzStart>:<cmuStart>|<askStart>|<cmdStart>|<callStart>|<mscStart>|<navStart>;

//////////////////////////////////////前缀相关//////////////////////////////////////

<preAll>:<preActive>|<preAllPls>;
<preAllPls>:[<prePlease>]<prePassive>;

<preActive>:我要|我想|我想要;
<prePassive>:帮我|替我|给我|帮|帮忙|你给我|你替我|你;
<preTell>:告诉我|你告诉我;
<prePlease>:请|麻烦;

//////////////////////////////////////交流相关//////////////////////////////////////

<cmuStart>:<cmuKeywords>;

<cmuKeywords>:你好;

//////////////////////////////////////指令相关//////////////////////////////////////

<cmdStart>:<cmdCmd>|<cmdApp>;

<cmdCmd>:[<preAll>]<cmdKeywords>;
<cmdApp>:[<preAll>]<cmdAppOpen><cmdAppNames>;
<cmdKeywords>:打开音乐|关闭音乐;
<cmdAppOpen>:打开|启动|运行;
<cmdAppNames>:电子狗;

//////////////////////////////////////呼叫相关//////////////////////////////////////

<callStart>:<callActTo>|<callActCmd>|<callActPh>;

//给xxx的手机打个电话
<callActTo>:[<preAll>]<callTo><callTarget>[[<callOf>]<callPhone>][<callActToSuf>]<callPhoneSuf>;
//打电话给xxx的手机
<callActCmd>:[<preAll>]<callCmd><callTarget>[[<callOf>]<callPhone>];
//打xxx的手机
<callActPh>:[<preAll>]<callMake><callTarget>[<callOf>]<callPhone>;

//呼叫的目标，支持联系人或数字串或字母串
//<callTarget>:<callExtract>|<callEnglish>|<allChaos>;
<callTarget>:<callExtract>|<callEnglish>;
<callExtract>:<callCon1>|<callCon2>|[<callPrefix>]<callList>[<callSuffix>]|<callService>|<callNumber>;
//数字串，暂定最多13位
<callNumber>:<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>[<allDigit>]]]]]]]]]]]];
//字母串，暂定最多12个
<callEngAll>:<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>[<allAlpha>]]]]]]]]]]];
//电话类型
<callPhone>:<callPhoneA>|<callPhoneZ>|<callPhoneM>|<callPhoneS>;
//联系人列表
//<callList>:<callUsual>|<callUnusual>|<callEngAll>;
<callList>:<callUsual>|<callUnusual>;

<callActToSuf>:一|个|一个|打|打个|打一个|拨打|拨打个|拨打一个;
<callPhoneSuf>:电话;
<callCmd>:打电话给|打个电话给|打一个电话给|拨号给|呼叫|打给|电话给|联系;
<callMake>:拨打|拨|打;
<callPro>:移动|联通|电信|铁通;
<callPhoneA>:号码|电话|电话号|电话号码;
<callPhoneZ>:座机|座机号|座机号码|固定电话|固话;
<callPhoneM>:手机|手机号|手机号码;
<callPhoneS>:短号|亲情号|亲情短号|亲情号码|集团号|集团号码|集团短号;
<callTo>:给;
<callOf>:的;
<callCon1>:张三;
<callCon2>:张三;
<callUsual>:李四;
<callUnusual>:王二;
<callService>:服务号;
<callEnglish>:happy;
<callPrefix>:同行者;
<callSuffix>:腾讯;


//////////////////////////////////////导航相关//////////////////////////////////////

<navStart>:<navTo>|<navCmd>|<navHow>|<navSearch>|<navHave>;

//导航到xxx去
<navTo>:[<preAll>][<navNav>]<navToWords><navTarget><navSuffix>;
//导航去xxx
<navCmd>:[<preAll>][<navNav>]<navGo>;
<navGo1>:<navCmdWord><navTarget>;
<navGo2>:[<navSuffix>]<navNearAct>;
<navGo>:<navGo1>|<navGo2>;
//告诉我xxx怎么走
<navHow>:[[<prePlease>]<preTell>]<navTarget><navWhere>;
//[附近]搜索[一下][附近[的]]xxx[怎么走]
<navSearch>:[<preAll>][<navNear>]<navFind>[<navAmount>]<navTarget>[<navWhere>];
//[附近]搜索[一下][附近]有什么xxx
<navHave>:[<preAll>][<navNear>][<navFind>[<navAmount>]][<navNear>]<navWhere><navTarget>;

//导航目标，支持家或公司或POI列表或周边
//<navTarget>:<navHome>|<navCompany>|<navNearTar>|<navPOI>|<allChaos>;
<navTarget>:<navHome>|<navCompany>|<navNearTar>|<navPOI>;

//附近的目标
<navNearTar>:[<navNear>[<navOf>]]<navNearKw>;

<navNav>:导航|领路|带我|领我|引我|带领|带领我;
<navWhere>:怎么走|怎么去|怎么到|在哪里|哪有|哪里有;
<navCmdWord>:到|去|回|至|的;
<navToWords>:到|回;
<navSuffix>:去;
<navNear>:附近|最近|就近|旁边|周围|周边;
<navFind>:查找|查|查询|找|搜索|搜;
<navAmount>:下|一下|个|一个;
<navOf>:的;
<navHome>:家|家里;
<navCompany>:公司|单位;
<navPOI>:世界之窗;
<navNearAct>:加油;
<navNearKw>:加油站;


//////////////////////////////////////音乐相关//////////////////////////////////////

//<mscStart>:[[<preAll>]<mscCmd>[<mscAmount>]]<mscTarget>;
<mscStart>:[<preAll>]<mscCmd>[<mscAmount>]<mscTarget>;

<mscTarget>:<mscTarT>|<mscTarL>|<mscTarS>|<mscTarA>;
//[xxx的]xxx[类型][的歌]
<mscTarT>:[<mscSinger>[<mscOf>]]<mscType>[<mscTSuf>][[<mscOf>]<mscSong>];
//xxx的xxx
<mscTarL>:[<mscSinger>[<mscOf>]]<mscList>[<mscSSuf>];
//xxx的歌
<mscTarS>:<mscSinger>[<mscOf><mscSong>];
//[xxx的]xxx专辑
<mscTarA>:[<mscSinger>[<mscOf>]]<mscAlbum>[<mscASuf>];


<mscCmd>:听|播放|播|放|来|点播|点|收听;
<mscAmount>:个|一个|首|一首|曲|一曲|一;
<mscSinger>:陈奕迅;
<mscAlbum>:明年今日;
<mscList>:十年;
<mscSong>:歌|歌曲|音乐|曲|曲子;
<mscOf>:的;
<mscType>:摇滚;
<mscTSuf>:流派|类|类型|风格;
<mscASuf>:专辑;
<mscSSuf>:这首歌;


//////////////////////////////////////新闻相关//////////////////////////////////////


<nwsStart>:[[<preAll>]<nwsCmd>[<nwsAmount>]]<nwsTarget>;

//一段关于xxx相关的新闻
<nwsTarget>:[[<nwsInc>]<nwsKw>[<nwsRef>][<nwsOf>]]<nwsNews>;

<nwsCmd>:听|播放|播|放|来|点播|点|收听|打开;
<nwsAmount>:个|一个|段|一段|则|一则|一;
<nwsInc>:包含|关于|有关;
<nwsRef>:有关|相关|类|类型|分类;
<nwsKw>:娱乐|同行者;
<nwsNews>:新闻;
<nwsOf>:的;


//////////////////////////////////////现在播放的是什么音乐//////////////////////////////////////
<askStart>:<askMsc>;

<askMsc>:<askMsc1>|<askMsc2>|<askMsc3>;
//请告诉我当前播放的歌是什么歌
<askMsc1>:[[<prePlease>]<preTell>]<askMscNow><askMscPlay>[<askMscOf>[<askMscMsc>]<askMscIs>]<askMscWhat>[<askMscMsc>];
//请告诉我这是播的什么歌
<askMsc2>:[[<prePlease>]<preTell>]<askMscNow>[<askMscIs>[<askMscPlay>[<askMscOf>]]]<askMscWhat><askMscMsc>;
//请告诉我这歌是什么歌
<askMsc3>:[[<prePlease>]<preTell>]<askMscNow><askMscMsc>[<askMscIs>]<askMscWhat>[<askMscMsc>];

<askMscNow>:现在|当前|正在|这;
<askMscPlay>:播放|播|放|收听|听;
<askMscOf>:的;
<askMscIs>:是|叫;
<askMscWhat>:什么|啥|嘛|谁的;
<askMscMsc>:歌|歌曲|音乐|曲|曲子;


//////////////////////////////////////混乱字符//////////////////////////////////////

<allDigit>:0|1|2|3|4|5|6|7|8|9;
//<allAlpha>:a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z;
<allAlpha>:A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z;

<allChaos>:<allCh01>[<allCh02>[<allCh03>[<allCh04>[<allCh05>]]]];
<allCh01>:的;
<allCh02>:的;
<allCh03>:的;
<allCh04>:的;
<allCh05>:的;

